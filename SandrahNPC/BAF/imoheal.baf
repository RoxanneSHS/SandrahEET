IF
Global("profs","LOCALS",0)
Class(Myself,MAGE_ALL)
GlobalLT("Chapter","Global",3)
THEN
RESPONSE #100
SetGlobal("profs","LOCALS",1)
ReallyForceSpellRES("CVImod",Myself)
END

IF
Alignment(Player1,MASK_EVIL)
Global("ImoHousew","GLOBAL",2)
ReputationGT(Player1,5)
ReputationLT(Player1,12)
InParty("CVSandr")
!AreaType(DUNGEON)
!See([ENEMY])
!AreaType(OUTDOOR)
GlobalGT("Chapter","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("ImoHousew","GLOBAL",3)
END

IF
GlobalLT("SanHealImo","GLOBAL",5)
InParty(Myself)
InParty("CVSandr")
OR(7)
SpellCastOnMe("CVSandr",CLERIC_CURE_LIGHT_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_MEDIUM_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_CRITICAL_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_HEAL)
SpellCastOnMeRES("Spin109","CVSandr")
SpellCastOnMeRES("SanHand","CVSandr")
SpellCastOnMeRES("cvwoun","CVSandr")
THEN
RESPONSE #100
IncrementGlobal("SanHealImo","GLOBAL",1)
END

IF
InParty(Myself)
InParty("CVSandr")
OR(3)
Global("SanHealImo","GLOBAL",7)
Global("SanHealImo","GLOBAL",5)
Global("ImoHousew","GLOBAL",3)
!See([ENEMY])
CombatCounter(0)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
THEN
RESPONSE #100
MoveToObject("CVSandr")
Interact(Player1)
END

IF
Global("OrcPrison","GLOBAL",14)
THEN
RESPONSE #100 
StartCutSceneMode()
SetGlobal("OrcPrison","GLOBAL",15)
ActionOverride("CVSandr",DestroyItem("SanHenHu"))
CreateVisualEffectObject("SPDIMNDR","CVSandr")
Wait(3)
FadeToColor([30.0],0) 
Wait(4) 
ActionOverride("CVSandr",LeaveParty())
Wait(6)
ActionOverride("CVSandr",MoveBetweenAreas("BG1804",[1036.182],0))
ActionOverride("Pellig",MoveBetweenAreas("BG1804",[984.279],1))
Wait(2)
FadeFromColor([30.0],0) 
Wait(2)
EndCutSceneMode()
SetGlobal("SandrahJoined","GLOBAL",15)
END

IF
GlobalGT("OrcPrison","GLOBAL",10)
AreaCheck("CVorc2")
HasItem("ImoSlHP1",Myself)
THEN
RESPONSE #100
DestroyItem("ImoSlHP1")
END

IF
Global("ImoenSpawned","GLOBAL",1)
Global("SanHealImo","GLOBAL",6)
OR(7)
SpellCastOnMe("CVSandr",CLERIC_CURE_LIGHT_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_MEDIUM_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_CRITICAL_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_HEAL)
SpellCastOnMeRES("Spin109","CVSandr")
SpellCastOnMeRES("SanHand","CVSandr")
SpellCastOnMeRES("cvwoun","CVSandr")
THEN
RESPONSE #100
SetGlobal("SanHealImo","GLOBAL",7)
END

IF
GlobalGT("RevealBandits","GLOBAL",0)
OR(2)
Global("BANDITENEMY","GLOBAL",1)
Dead("Tranzig")
InParty("CVSandr")
GlobalLT("GullCave","GLOBAL",2)
GlobalLT("RevealBandits","GLOBAL",9)
GlobalGT("SanImoNashInt","GLOBAL",7)
GlobalLT("SanImoNashInt","GLOBAL",18)
!Dead("Tazok")
THEN
RESPONSE #100
SetGlobal("RevealBandits","GLOBAL",9)
SetGlobal("BANDITENEMY","GLOBAL",0)
HideAreaOnMap("BG1900")
END

IF
Global("SanImoNashInt","GLOBAL",17)
Global("GullCave","GLOBAL",2)
Global("RevealBandits","GLOBAL",9)
THEN
RESPONSE #100
RevealAreaOnMap("BG1900")
SetGlobal("RevealBandits","GLOBAL",1)
END

IF
Global("Sandrahjoined","Global",83)
!InMyArea("CVSandr")
!See([ENEMY])
CombatCounter(0)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("SanPCIdiot","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("SanPCIdiot","LOCALS",1)
Interact(Player1)
END

IF
Global("X#IMPC8","GLOBAL",2)
THEN
RESPONSE #100
SetGlobal("X#IMPC8","GLOBAL",3)
AddSpecialAbility("spcl919")
END