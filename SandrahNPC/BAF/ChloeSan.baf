//Chloe
IF
InParty("RChloe")
GlobalLT("SanCloeRom","GLOBAL",2)
!AreaType(DUNGEON)
Global("SanCloInGr","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("SanCloInGr","LOCALS",1)
END

IF
InParty("RChloe")
Global("SanCloeRom","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("SanCloeRom","GLOBAL",1)
RealSetGlobalTimer("SanCloeInterv","LOCALS",1800)
END

IF 
InParty("RChloe")
InMyArea("Pellig")
AreaType(OUTDOOR)
!AreaType(DUNGEON)
Global("SanCloeRom","GLOBAL",1)
RealGlobalTimerExpired("SanCloeInterv","LOCALS")
THEN
RESPONSE #100
SetGlobal("SanCloeRom","GLOBAL",2)
END

IF
InParty("RChloe")
Global("SanCloeRom","GLOBAL",3)
RealGlobalTimerExpired("SanCloeInterv","LOCALS")
AreaType(DUNGEON)
!See([ENEMY])
See("RChloe")
THEN
RESPONSE #100
SetGlobal("SanCloeRom","GLOBAL",4)
END

IF
InParty("RChloe")
RealGlobalTimerExpired("SanCloeInterv","LOCALS")
AreaType(OUTDOOR)
!AreaType(DUNGEON)
TimeGT(18)
OR(2)
Global("SanCloeRom","GLOBAL",5)
Global("SanCloeRom","GLOBAL",11)
THEN
RESPONSE #100
IncrementGlobal("SanCloeRom","GLOBAL",1)
END

IF
InParty("RChloe")
Global("SanCloeRom","GLOBAL",9)
RealGlobalTimerExpired("SanCloeInterv","LOCALS")
AreaType(OUTDOOR)
TimeGT(21)
THEN
RESPONSE #100
SetGlobal("SanCloeRom","GLOBAL",10)
END

IF
GlobalGT("SanCloeRom","GLOBAL",12)
Global("SanDra16","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("SanDra16","LOCALS",1)
AddSpecialAbility("CVDra16")
END

IF
Global("SanCloeRom","GLOBAL",13)
RealGlobalTimerExpired("SanCloeInterv","LOCALS")
AreaType(FOREST)
InParty("RChloe")
!See([ENEMY])
Delay(18)
THEN
RESPONSE #100
ClearAllActions()
StartCutSceneMode()
SetGlobal("SanCloeRom","GLOBAL",14)
CreateCreatureObjectOffset("sahobwiz",Myself,[100.100])
CreateCreatureObjectOffset("hobwiz01",Myself,[-100.-100])
CreateCreatureObjectOffset("hobwiz02",Myself,[100.-100])
CreateCreatureObjectOffset("sachwiz",Myself,[-100.100])
CreateCreatureObjectOffset("hobelite",Myself,[0.0])
EndCutSceneMode()
END

IF
Dead("sachwiz")
Global("SanCloeRom","GLOBAL",14)
InParty("RChloe")
!Dead("RChloe")
THEN
RESPONSE #100
SetGlobal("SanCloeRom","GLOBAL",15)
END

IF
InParty("RChloe")
Global("SanCloeRom","GLOBAL",16)
!Global("SanYasfr","GLOBAL",12)
!AreaType(DUNGEON)
RealGlobalTimerExpired("SanCloeInterv","LOCALS")
PartyRested()
TimeGT(4)
TimeLT(12)
THEN
RESPONSE #100
SetGlobal("SanCloeRom","GLOBAL",17)
END

IF
Global("EdwinBanter1CR","GLOBAL",2)
InParty("Edwin")
InParty("RChloe")
GlobalGT("Cowscroll","GLOBAL",8)
GlobalGT("SanCloeRom","GLOBAL",6)
Global("SanChlEd","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("SanChlEd","LOCALS",1)
END

IF
GlobalGT("SanCloeRom","GLOBAL",8)
Global("SanCloeBryn","LOCALS",0)
InParty("RChloe")
!InParty("Imoen2")
Global("Chapter","GLOBAL",16)
OR(7)
AreaCheck("ar1604")
AreaCheck("ar1603")
AreaCheck("ar1606")
AreaCheck("ar1609")
AreaCheck("ar1611")
AreaCheck("ar1612")
AreaCheck("ar1613")
THEN
RESPONSE #100
SetGlobal("SanCloeBryn","LOCALS",1)
END

IF
Global("SanCloeBryn","LOCALS",2)
GlobalGT("SanCloeRom","GLOBAL",8)
GlobalGT("SanHealImo","GLOBAL",8)
RealGlobalTimerExpired("SanCloeInterv","LOCALS")
InParty("RChloe")
InParty("Imoen2")
THEN
RESPONSE #100
SetGlobal("SanCloeBryn","LOCALS",3)
END

IF
!See([ENEMY])
See("RChloe")
!StateCheck("RChloe",CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck(Myself,CD_STATE_NOTVALID)
OR(11)
Global("SanCloeRom","GLOBAL",2)
Global("SanCloeRom","GLOBAL",4)
Global("SanCloeRom","GLOBAL",6)
Global("SanCloeRom","GLOBAL",8)
Global("SanCloeRom","GLOBAL",10)
Global("SanCloeRom","GLOBAL",12)
Global("SanCloeRom","GLOBAL",15)
Global("SanCloeRom","GLOBAL",17)
Global("SanCloeBryn","LOCALS",1)
Global("SanChlEd","LOCALS",1)
Global("SanCloeBryn","LOCALS",3)
THEN
RESPONSE #100
MoveToObject("RChloe")
Interact("CVSandr")
END

IF
Global("SanCloeBryn","LOCALS",4)
THEN
RESPONSE #100
SetGlobal("SanCloeBryn","LOCALS",5)
ReallyForceSpellRES("CVPro11",Myself)
END