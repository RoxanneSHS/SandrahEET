IF
GlobalLT("SanHealChlo","GLOBAL",9)
InParty(Myself)
InParty("CVSandr")
OR(7)
SpellCastOnMe("CVSandr",CLERIC_CURE_LIGHT_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_MEDIUM_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_CRITICAL_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_HEAL)
SpellCastOnMeRES("Spin109","CVSandr")
SpellCastOnMeRES("SanHand","CVSandr")
SpellCastOnMeRES("cvwoun","CVSandr")
THEN
RESPONSE #100
IncrementGlobal("SanHealChlo","GLOBAL",1)
END

IF
Global("MyClass","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("MyClass","LOCALS",1)
AddKit("YRKENSAI")
END

IF 
InParty(Myself)
InParty("CVSandr")
!See([ENEMY])
CombatCounter(0)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
OR(3)
Global("SanHealChlo","GLOBAL",3)
Global("SanHealChlo","GLOBAL",7)
Global("SanHealChlo","GLOBAL",13)
THEN
RESPONSE #100
MoveToObject("CVSandr")
Dialog("CVSandr")
END

IF
Global("SanHealChlo","GLOBAL",9)
InParty("CVSandr")   
GlobalGT("SanCloeRom","GLOBAL",10)
AreaType(OUTDOOR)
!AreaType(CITY)
!See([ENEMY])
CombatCounter(0)
Delay(12)
THEN
RESPONSE #100
SetGlobal("SanHealChlo","GLOBAL",10)
CreateCreatureObjectOffset("SanSuWi",Myself,[100.100])
CreateCreatureObjectOffset("SanSuW2",Myself,[-100.-100])
CreateCreatureObjectOffset("SanSuW3",Myself,[100.-100])
CreateCreatureObjectOffset("SanSuWL",Myself,[50.50])
END

IF
Global("SanHealChlo","GLOBAL",11)
THEN
RESPONSE #100
StartCutSceneMode()
ActionOverride("SanSuWL",ChangeEnemyAlly(Myself,ENEMY))
ActionOverride("SanSuWi",ChangeEnemyAlly(Myself,ENEMY))
ActionOverride("SanSuW2",ChangeEnemyAlly(Myself,ENEMY))
ActionOverride("SanSuW3",ChangeEnemyAlly(Myself,ENEMY))
ActionOverride("SanSuWL",Attack(Player1))
ActionOverride("SanSuWi",Attack(Player2))
ActionOverride("SanSuW2",Attack(Player3))
ActionOverride("SanSuW3",Attack(Player4))
CreateCreatureObjectOffset("CVCloMi1",Myself,[140.-140])
CreateCreatureObjectOffset("Icmin01",Myself,[-140.100])
EndCutSceneMode()
SetGlobal("SanHealChlo","GLOBAL",12)
ActionOverride("CVSandr",RealSetGlobalTimer("SanCloeInterv","LOCALS",1800))
END

IF
Global("SanHealChlo","GLOBAL",12)
Dead("CVCloMi1")
THEN
RESPONSE #100
SetGlobal("SanHealChlo","GLOBAL",13)
END

IF
!HasItem("CHLOSW1",Myself) 
Global("IHaveSW1CR","LOCALS",1)
AreaCheck("ar4286")
THEN
RESPONSE #100
GiveItemCreate("CHLOSW1","RChloe",0,0,0)
END

IF
!HasItem("CHLOSW2",Myself)
Global("IHaveSW2CR","LOCALS",1)
AreaCheck("ar4286")
THEN
RESPONSE #100
GiveItemCreate("CHLOSW2","RChloe",0,0,0)
END

IF
!HasItem("CHLOSW3",Myself)
Global("IHaveSW3CR","LOCALS",1)
AreaCheck("ar4286")
THEN
RESPONSE #100
GiveItemCreate("CHLOSW3","RChloe",0,0,0)
END

IF
!HasItem("CHLOSW4",Myself)
Global("IHaveSW4CR","LOCALS",1)
AreaCheck("ar4286")
THEN
RESPONSE #100
GiveItemCreate("CHLOSW4","RChloe",0,0,0)
END