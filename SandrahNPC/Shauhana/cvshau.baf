// Fight Script

IF
See(Nearest([PC]))
NumTimesTalkedTo(0)
THEN
RESPONSE #100
EquipRanged()
END

IF
ActionListEmpty()
OR(4)
See(NearestEnemyOfType([0.0.0.LONG_BOW]))
See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
See(NearestEnemyOfType([0.0.0.DRUID_ALL]))
See(NearestEnemyOfType([0.0.0.BARD_ALL]))
!Range(LastSeenBy(Myself),10)
THEN
RESPONSE #100
EquipRanged()
AttackReevaluate(LastSeenBy(Myself),30)
END

IF
ActionListEmpty()
OR(4)
See(NearestEnemyOfType([0.0.0.LONG_BOW]))
See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
See(NearestEnemyOfType([0.0.0.DRUID_ALL]))
See(NearestEnemyOfType([0.0.0.BARD_ALL]))
THEN
RESPONSE #100
EquipMostDamagingMelee()
AttackReevaluate(LastSeenBy(Myself),30)
END

// Unveil after rest

IF
PartyRested()
Global("ShauVeil","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("ShauVeil","GLOBAL",0)
ReallyForceSpellRES("Shauunve",Myself)
END

IF
Global("SanShauMeet","GLOBAL",2)
See(Player1)
THEN
RESPONSE #100
Dialogue(Nearest([PC]))
END

//Velvetfoot
IF
AreaCheck("ar3521")
Global("ShauVelvft","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("ShauVelvft","LOCALS",1)
END

// Shauhana Story Talks

IF
Global("SanShauMeet","GLOBAL",3)
InParty("CVShau")
AreaCheck("ar3520")
THEN
RESPONSE #100
SetGlobal("SanShauMeet","GLOBAL",4)
EraseJournalEntry(@349)
END

IF
Global("SanShauMeet","GLOBAL",4)
CombatCounter(0)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogNoSet("CVShau")
END

IF
Global("SanShauMeet","GLOBAL",6)
!See([Enemy])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
CombatCounter(0)
THEN
RESPONSE #100
StartDialogNoSet("CVShau")
END

IF
Global("SanShauOrcStaff","GLOBAL",5)
!AreaCheck("CVSHA4")
!AreaCheck("CVSHA8")
InParty("CVSandr")
THEN
RESPONSE #100
SetGlobal("SanShauOrcStaff","GLOBAL",6)
END


//Road Out of Clan's Land

IF
Global("SanShauOrcStaff","GLOBAL",2)
AreaCheck("CVSHA4")
THEN
RESPONSE #100
SetGlobal("SanShauOrcStaff","GLOBAL",3)
END

IF
Global("SanShauOrcStaff","GLOBAL",11)
AreaCheck("CVSHA4")
PartyHasItem("CVORSTA")
THEN
RESPONSE #100
StartCutSceneMode()
Wait(2)
CreateCreatureObjectOffset("CVSHPRI",Player1,[50.50])
CreateCreature("icegl2",[3370.2267],5)
CreateCreature("icegl2",[3299.2514],0)
CreateCreature("sanclaho",[3147.2263],5)
CreateCreature("sanclaho",[3152.2321],12)
EndCutSceneMode()
SetGlobal("SanShauOrcStaff","GLOBAL",12)
END

IF
Global("SanShauOrcStaff","GLOBAL",13)
Dead("CVShpri")
!PartyHasItem("CVSHAJWL")
Delay(10)
THEN
RESPONSE #100
SetGlobal("SanShauOrcStaff","GLOBAL",14)
END

// Start PID

IF
Global("SanShauMeet","GLOBAL",9)
Global("ShauPid","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("ShauPid","GLOBAL",1)
END

// Sharkta Fai Talks

IF
Global("SanSharkta","GLOBAL",1)
PartyHasItem("Shaubo")
InParty("CVSandr")
!AreaCheck("CVSHA1")
!AreaCheck("ar3520")
!AreaType(DUNGEON)
THEN
RESPONSE #100
SetGlobal("SanSharkta","GLOBAL",2)
END

IF
Global("SanSharkta","GLOBAL",3) 
Global("SanItmChg","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("SanItmChg","LOCALS",1)
TakePartyItem("Shaubo") 
DestroyItem("Shaubo") 
GiveItemCreate("Shaubo2","CVSandr",0,0,0) 
RealSetGlobalTimer("ShaktTime","GLOBAL",800)
END

IF
Global("SanSharkta","GLOBAL",3) 
RealGlobalTimerExpired("ShaktTime","GLOBAL")
PartyHasItem("Shaubo2")
InParty("CVSandr")
!AreaType(DUNGEON)
THEN
RESPONSE #100
SetGlobal("SanSharkta","GLOBAL",4)
END

IF
Global("SanSharkta","GLOBAL",3) 
InParty("CVSandr")
AreaCheck("CVSh10")
PartyHasItem("CVSHAJWL")
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("SanSharkta","GLOBAL",4)
END

IF
Global("SanSharkta","GLOBAL",5)
PartyHasItem("CVSHAJWL")
AreaCheck("CVSha3")
InParty("CVSandr")
THEN
RESPONSE #100
SetGlobal("SanSharkta","GLOBAL",6)
END

IF
Global("SanSharkta","GLOBAL",11)
THEN
RESPONSE #100
SetGlobal("ShauVeil","GLOBAL",2)
CreateVisualEffectObject("SPFLESHS","CVShau")
ReallyForceSpellRES("Shauheal",Myself)
SetGlobal("SanSharkta","GLOBAL",12)
END

IF
AreaCheck("CVSha7")
!PartyHasItem("Shamyrk")
Global("Shamyrkfound","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("Shamyrkfound","GLOBAL",1)
END

IF
InParty("CVSandr")
PartyHasItem("Shamyrk")
Global("Shamyrkfound","GLOBAL",2)
GlobalGT("SanSharkta","GLOBAL",12)
!AreaCheck("CVSHA7")
THEN
RESPONSE #100
SetGlobal("Shamyrkfound","GLOBAL",3)
END  

IF
InParty("CVSandr")
Global("Shamyrkfound","GLOBAL",4)
RealGlobalTimerExpired("ShaktTime","GLOBAL")
!AreaType(OUTDOOR)
!AreaType(DUNGEON)
THEN
RESPONSE #100
SetGlobal("Shamyrkfound","GLOBAL",5)
END

IF
!See([Enemy])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
CombatCounter(0)
Global("SanSharkta","GLOBAL",8)
THEN
RESPONSE #100
MoveToObject(Player1)
Interact("CVShau")
END

IF
!See([Enemy])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
CombatCounter(0)
Global("SanSharkta","GLOBAL",12)
THEN
RESPONSE #100
Interact("CVShau")
END

// Reaching Amn

IF
AreaCheck("ar0020")
InParty("CVSandr")
Global("Shaucity","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("Shaucity","GLOBAL",1)
END

IF
Global("Shaucity","GLOBAL",1)
THEN
RESPONSE #100
Interact("CVShau")
END

IF
Global("Shaucity","GLOBAL",3)
THEN
RESPONSE #100
SetGlobal("Shaucity","GLOBAL",4)
RealSetGlobalTimer("ShauOrwSpawn","LOCALS",4120)
END

IF
Global("Shaucity","GLOBAL",4)
RealGlobalTimerExpired("ShauOrwSpawn","LOCALS")
!TimeOfDay(DAY)
InParty("CVSandr")
OR(7)
AreaCheck("ar0400")
AreaCheck("ar0300")
AreaCheck("ar0500")
AreaCheck("ar0700")
AreaCheck("ar0800")
AreaCheck("ar0900")
AreaCheck("ar1000")
THEN
RESPONSE #100
CreateCreatureObjectOffset("CVorwarl","CVShau",[50.50])
SetGlobal("Shaucity","GLOBAL",5)
END

IF
OR(3)
AreaCheck("ar0501")
AreaCheck("ar0502")
AreaCheck("ar0503")
Global("ShauSkin","LOCALS",0)
PartyHasItem("Misc6a")
THEN
RESPONSE #100
SetGlobal("ShauSkin","LOCALS",1)
END

IF
GlobalTimerExpired("ShauToClan","GLOBAL")
!AreaCheck("CVSha1")
!AreaCheck("ar3520")
!See([Enemy])
CombatCounter(0)
Global("ShauRemSH","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("ShauRemSH","LOCALS",1)
END

//Shauhana and Minsc
IF
Global("SanShauMeet","GLOBAL",5)
Global("SanShauMinsc","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",1)
RealSetGlobalTimer("SanShauMinTi","GLOBAL",2400)
END

IF
InParty("CVSandr")
!InParty("Minsc")
!AreaCheck("CVSha1")
Global("SanShauMinsc","GLOBAL",1)
OR(2)
GlobalGT("Shausc","LOCALS",0)
GlobalGT("ShauBrag","LOCALS",0)
RealGlobalTimerExpired("SanShauMinTi","GLOBAL")
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",2)
END

IF
InParty("CVSandr")
!InParty("Minsc")
!Dead("Minsc")
Global("SanShauMinsc","GLOBAL",3)
PartyHasItem("CVORSTA")
GlobalGT("SanSharkta","GLOBAL",15)
AreaCheck("CVSHA3")
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",4)
END

IF
InParty("CVSandr")
InParty("Minsc")
Global("SanShauMinsc","GLOBAL",1)
RealGlobalTimerExpired("SanShauMinTi","GLOBAL")
OR(11)
AreaCheck("CVSHA1")
AreaCheck("CVSHA2")
AreaCheck("CVSHA3")
AreaCheck("CVSHA4")
AreaCheck("CVSHA5")
AreaCheck("CVSHA6")
AreaCheck("CVSHA7")
AreaCheck("CVSHA8")
AreaCheck("CVSHA9")
AreaCheck("CVSH10")
AreaCheck("ar3520")
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",6)
END


IF
Global("ShauMeetAmn","GLOBAL",1)
!InParty("Minsc")
See("Minsc")
GlobalGT("SanShauMinsc","GLOBAL",4)
GlobalLT("SanShauMinsc","GLOBAL",11)
InParty(Myself)
AreaCheck("ar0406")
InParty("CVSandr")
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",10)
END

IF
!Global("ShauMeetAmn","GLOBAL",1)
!InParty("Minsc")
See("Minsc")
GlobalGT("SanShauMinsc","GLOBAL",4)
GlobalLT("SanShauMinsc","GLOBAL",11)
InParty(Myself)
AreaCheck("ar0406")
InParty("CVSandr")
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",8)
END

IF
Global("SanShauMinsc","GLOBAL",11)
!InParty("Minsc")
!AreaCheck("ar0406")
InParty("CVSandr")
Global("ShauPCRejMinsc","LOCALS",0)
Delay(15)
THEN
RESPONSE #100
SetGlobal("ShauPCRejMinsc","LOCALS",1)
END

IF
InParty("CVSandr")
InParty("Minsc")
AreaType(OUTDOOR)
GlobalGT("SanShauMinsc","GLOBAL",6)
GlobalLT("SanShauMinsc","GLOBAL",12)
RealGlobalTimerExpired("SanShauMinTi","GLOBAL")
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",12)
END

IF
InParty("CVSandr")
InParty("Minsc")
AreaType(OUTDOOR)
TimeOfDay(DAY)
Global("SanShauMinsc","GLOBAL",15)
RealGlobalTimerExpired("SanShauMinTi","GLOBAL")
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",16)
END

IF
InParty("CVSandr")
InParty("Minsc")
AreaType(CITY)
TimeOfDay(DAY)
Global("SanShauMinsc","GLOBAL",17)
RealGlobalTimerExpired("SanShauMinTi","GLOBAL")
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",18)
END

IF
Global("SanShauMinsc","GLOBAL",18)
!See([Enemy])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck("Minsc",CD_STATE_NOTVALID)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",19)
CreateCreatureObjectOffScreen("x#setta","CVShau",400,400,0)
SmallWait(2)
ActionOverride("x#setta",MoveToObject("Minsc"))
END

IF
Global("SanShauMinsc","GLOBAL",19)
See("x#setta")
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",20)
END

IF
Global("WWboogone","GLOBAL",2)
Global("SanShaubogon","LOCALS",0)
AreaCheck("ar1000")
THEN
RESPONSE #100
SetGlobal("SanShaubogon","LOCALS",1)
END

IF
Global("SanShaubogon","LOCALS",1)
InParty("Minsc")
!AreaCheck("Ar1000")
!See([Enemy])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck("Minsc",CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("SanShaubogon","LOCALS",2)
CreateCreatureObjectOffset("CVbootlk","Minsc",[25.25])
Interact("CVShau")
END

IF
Global("SanShauMinsc","GLOBAL",21)
HPPercentLT("Minsc",85)
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",22)
END

IF
Global("SanShauMinsc","GLOBAL",23)
RealGlobalTimerExpired("SanShauMinTi","GLOBAL")
InParty("Minsc")
InParty("CVSandr")
!AreaType(DUNGEON)
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",24)
END

IF
Global("SanShauMinsc","GLOBAL",25)
RealGlobalTimerExpired("SanShauMinTi","GLOBAL")
InParty("Minsc")
AreaType(OUTDOOR)
!AreaType(DUNGEON)
!AreaType(CITY)
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",26)
END

IF
Global("SanShauMinsc","GLOBAL",30)
InParty("Minsc")
GlobalGT("ShauOrcWar","GLOBAL",1)
!AreaCheck("CVSHA3")
Delay(12)
THEN
RESPONSE #100
SetGlobal("SanShauMinsc","GLOBAL",31)
END

IF
OR(2)
AreaCheck("ar4303")
AreaCheck("ar4304")
InParty("Minsc")
Global("Orcbandits","LOCALS",0)
Delay(12)
THEN
RESPONSE #100
SetGlobal("Orcbandits","LOCALS",1)
END



//___Dialogue Start_____

IF
!See([Enemy])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
CombatCounter(0)
OR(10)
Global("SanSharkta","GLOBAL",2)
Global("SanSharkta","GLOBAL",4)
Global("SanSharkta","GLOBAL",6)
Global("SanShauOrcStaff","GLOBAL",6)
Global("Shamyrkfound","GLOBAL",1)
Global("Shamyrkfound","GLOBAL",3)
Global("Shamyrkfound","GLOBAL",5)
Global("SanShauMinsc","GLOBAL",2)
Global("SanShauMinsc","GLOBAL",4)
Global("SanShauMinsc","GLOBAL",6)
THEN
RESPONSE #100
MoveToObject("CVSandr")
Interact("CVShau")
END

IF
!See([Enemy])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck("Minsc",CD_STATE_NOTVALID)
CombatCounter(0)
InParty("Minsc")
OR(8)
Global("SanShauMinsc","GLOBAL",12)
Global("SanShauMinsc","GLOBAL",14)
Global("SanShauMinsc","GLOBAL",16)
Global("SanShauMinsc","GLOBAL",20)
Global("SanShauMinsc","GLOBAL",22)
Global("SanShauMinsc","GLOBAL",26)
Global("SanShauMinsc","GLOBAL",31)
Global("Orcbandits","LOCALS",1)
THEN
RESPONSE #100
MoveToObject("Minsc")
Interact("CVShau")
END

IF
!See([Enemy])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
CombatCounter(0)
OR(7)
Global("SanShauOrcStaff","GLOBAL",1)
Global("SanShauOrcStaff","GLOBAL",3)
Global("ShauVelvft","LOCALS",1)
Global("SanShauOrcStaff","GLOBAL",14)
Global("ShauPCRejMinsc","LOCALS",1)
Global("ShauRemSH","LOCALS",1)
Global("ShauSkin","LOCALS",1)
THEN
RESPONSE #100
StartDialogNoSet("CVShau")
END