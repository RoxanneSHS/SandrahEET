IF
Global("SanKido","GLOBAL",0)
InParty("MWKido")
AreaCheck("ar0500")
CombatCounter(0)
Delay(12)
THEN
RESPONSE #100
IncrementGlobal("SanKido","GLOBAL",1)
MoveToObject(Player1)
Interact("CVSandr")
END

IF
Global("Kidopersonalquest","GLOBAL",4)
Global("SanKidoPlot","LOCALS",3)
InParty("MWKido")
AreaCheck("ar0400")
See("Door0408")
THEN
RESPONSE #100
GiveItemCreate("CVChSacr",Player1,0,0,0)
DisplayStringWait(Myself,@756)
ReallyForceSpellRES("SPCL336",Myself)
SetGlobal("SanKidoPlot","LOCALS",4)
END

IF
Global("SanKidoPlot","LOCALS",4)
PartyHasItem("CVChSacr")
AreaCheck("ar0400")
Global("SanCyricAng","ar0400",0)
THEN
RESPONSE #100
SetGlobal("SanCyricAng","ar0400",1)
END

IF
Global("Kiwantsdoquest","GLOBAL",1)
InParty("MWKido")
Global("SanKidoPlot","LOCALS",0)
!AreaCheck("ar0300")
CombatCounter(0)
AreaType(CITY)
Delay(12)
THEN
RESPONSE #100
SetGlobal("SanKidoPlot","LOCALS",1)
MoveToObject(Player1)
Interact("CVSandr")
END

IF
RealGlobalTimerExpired("SanKidoTa","LOCALS")
InParty("MWKido")
OR(2)
Global("SanKido","GLOBAL",2)
Global("SanKido","GLOBAL",4)
THEN
RESPONSE #100
IncrementGlobal("SanKido","GLOBAL",1)
END

IF
RealGlobalTimerExpired("SanKidoTa","LOCALS")
InParty("MWKido")
Global("SanKido","GLOBAL",6)
OR(17)
AreaCheck("ar0314")
AreaCheck("ar0406")
AreaCheck("ar0509")
AreaCheck("ar0511")
AreaCheck("ar0513")
AreaCheck("ar0704")
AreaCheck("ar0709")
AreaCheck("ar1105")
AreaCheck("ar1602")
AreaCheck("ar2010")
AreaCheck("ar2203")
AreaCheck("ar4267")
AreaCheck("dd3302")
AreaCheck("ra5104")
AreaCheck("BG3200")
AreaCheck("ys0396")
AreaCheck("rr3305")
THEN
RESPONSE #100
IncrementGlobal("SanKido","GLOBAL",1)
END

IF
InParty(Myself)
InParty("MWKido")
!See([ENEMY])
See("CVSandr")
!StateCheck("MWKido",CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck(Myself,CD_STATE_NOTVALID)
OR(5)
Global("SanKido","GLOBAL",3)
Global("SanKido","GLOBAL",5)
Global("SanKido","GLOBAL",7)
Global("SanKido","GLOBAL",9)
Global("Kidopersonalquest","GLOBAL",19)
THEN
RESPONSE #100
MoveToObject("MWKido")
Interact("CVSandr")
END

IF
GlobalGT("Kidopersonalquest","GLOBAL",16)
AreaCheck("ar0408")
InParty("MWKido")
InMyArea("MWDolf")
!Dead("MWDolf")
THEN
RESPONSE #100
SetGlobal("Kidopersonalquest","GLOBAL",19)
END

IF
GlobalGT("secweareone","GLOBAL",1)
Global("SanKido","GLOBAL",8)
RealGlobalTimerExpired("SanKidoTa","LOCALS")
InParty("MWKido")
!AreaType(OUTDOOR)
THEN
RESPONSE #100
IncrementGlobal("SanKido","GLOBAL",1)
END