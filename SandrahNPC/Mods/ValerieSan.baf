IF
GlobalLT("SanHealValri","GLOBAL",3)
InParty(Myself)
InParty("CVSandr")
OR(7)
SpellCastOnMe("CVSandr",CLERIC_CURE_LIGHT_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_MEDIUM_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_CRITICAL_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_HEAL)
SpellCastOnMeRES("Spin109","CVSandr")
SpellCastOnMeRES("SanHand","CVSandr")
SpellCastOnMeRES("cvwoun","CVSandr")
THEN
RESPONSE #100
IncrementGlobal("SanHealValri","GLOBAL",1)
END

IF
Global("SanHealValri","GLOBAL",4)
InParty("CVSandr")
!AreaType(OUTDOOR)
!AreaType(DUNGEON)
GlobalGT("Chapter","GLOBAL",2)
Dead("Tranzig")
THEN
RESPONSE #100
SetGlobal("SanHealValri","GLOBAL",5)
END

IF
Global("SanHealValri","GLOBAL",8)
InParty("CVSandr")
!AreaType(OUTDOOR)
!AreaType(DUNGEON)
AreaType(CITY)
GlobalGT("Chapter","GLOBAL",4)
THEN
RESPONSE #100
SetGlobal("SanHealValri","GLOBAL",9)
END

IF
GlobalGT("SanGithSwJ","GLOBAL",3)
InParty("CVJenlig")
Global("ValJenB","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("ValJenB","LOCALS",1)
RealSetGlobalTimer("SanValriJ","LOCALS",970)
END

IF
Global("ValJenB","LOCALS",1)
RealGlobalTimerExpired("SanValriJ","LOCALS")
!AreaType(DUNGEON)
InParty("CVJenlig")
InParty("T2Val")
See("CVJenlig")
THEN
RESPONSE #100
SetGlobal("ValJenB","LOCALS",2)
END

IF
GlobalGT("T2ValFriendTalk","GLOBAL",14)
Global("SanHealValri","GLOBAL",6)
RealGlobalTimerExpired("SanValriJ","LOCALS")
InParty("CVSandr")
!AreaType(DUNGEON)
TimeOfDay(NIGHT)
AreaType(OUTDOOR)
See("CVSandr")
THEN
RESPONSE #100
SetGlobal("SanHealValri","GLOBAL",7)
END

IF
Global("SanHealValri","GLOBAL",10)
InParty("CVSandr")
GlobalGT("Chapter","GLOBAL",4)
GlobalGT("T2ValFriendTalk","GLOBAL",26)
OR(15)
AreaCheck("BG4801")
AreaCheck("BG2616")
AreaCheck("BG3351")
AreaCheck("BG3304")
AreaCheck("BG2301")
AreaCheck("BG0105")
AreaCheck("BG0116")
AreaCheck("BG0119")
AreaCheck("BG0706")
AreaCheck("BG4003")
AreaCheck("BG1001")
AreaCheck("bh0115")
AreaCheck("bh0107")
AreaCheck("BG3357")
AreaCheck("BG0114")
THEN
RESPONSE #100
SetGlobal("SanHealValri","GLOBAL",11)
END

IF
Global("SanHealValri","GLOBAL",12)
InParty("CVSandr")
Global("Sanrompath","GLOBAL",1)
GlobalGT("T2ValFriendTalk","GLOBAL",29)
AreaType(OUTDOOR)
!TimeOfDay(DAY)
THEN
RESPONSE #100
SetGlobal("SanHealValri","GLOBAL",13)
END

IF
OR(7)
Global("ValJenB","LOCALS",2)
Global("SanHealValri","GLOBAL",3)
Global("SanHealValri","GLOBAL",5)
Global("SanHealValri","GLOBAL",7)
Global("SanHealValri","GLOBAL",9)
Global("SanHealValri","GLOBAL",11)
Global("SanHealValri","GLOBAL",13)
InParty(Myself)
!See([ENEMY])
CombatCounter(0)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
THEN
RESPONSE #100
RealSetGlobalTimer("SanValri","LOCALS",1700)
Interact(Player1)
END